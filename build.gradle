buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.configFile = file "build.properties"

configFile.withReader {

    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

group = "bart.oilcraft"
version = "1.7.10"

minecraft {

    version = "1.7.10-10.13.0.1208"
    assetDir = "run/assets"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    appendix = 'universal'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
}

task incrementBuildNumber() {
    dependsOn "reobf"
    doLast {
        config.build = (config.build.toString().toInteger()) + 1

        configFile.withWriter {
            config.toProperties().store(it, "")
        }
    }
}